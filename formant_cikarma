# Olusturdugumuz textgrid in ismi
# (Formantlari iceren dosya ismiyle de ayni olmali)
	isim$ = "sesler"

# Kac adet timepoints (secilen saniyeler) var?
	num_timepoints = 10

# Textgrid dosyasinda kac dizi var?
	v_tier = 1

# Tablo olusturmak
	Create Table with column names: "formants", 0, "vowel time_index v_time time_abs F1 F2 F3"
	row_index = 0

# Zaman araliklarinin sayimi
	select TextGrid 'isim$'
	num_intervals = Get number of intervals: v_tier

# Aralıklar arasındaki döngü
   for interval_index from 1 to num_intervals
	select TextGrid 'isim$'
	label$ = Get label of interval: v_tier, interval_index

	# etiket bos olsa da devam et
	if label$ <> ""
		t_start = Get start time of interval: 1, interval_index
		t_end = Get end time of interval: 1, interval_index
		time_interval = (t_end - t_start)/(num_timepoints-1)

		selectObject: "Formant 'isim$'"

		# timepoints boyunca dongu bilgileri
		   for time_index from 1 to num_timepoints
			time_re_onset = (time_index-1)*time_interval
			current_time =  t_start + time_re_onset
			select Formant 'isim$'
			f1 = Get value at time: 1, current_time, "hertz", "Linear"
			f2 = Get value at time: 2, current_time, "hertz", "Linear"
			f3 = Get value at time: 3, current_time, "hertz", "Linear"

		     # tabloya bilgileri ekleme kismi
			select Table formants
			Insert row: row_index + 1
			row_index = row_index + 1
			Set string value: row_index, "vowel", label$
			Set numeric value: row_index, "time_index", time_index
			Set numeric value: row_index, "v_time", 'time_re_onset:3'
			Set numeric value: row_index, "time_abs", 'current_time:3'
			if f1 != undefined
				Set numeric value: row_index, "F1", 'f1:0'
			else
				Set string value: row_index, "F1", "NA"
			endif
			if f2 != undefined
				Set numeric value: row_index, "F2", 'f2:0'
			else
				Set string value: row_index, "F2", "NA"
			endif
			if f3 != undefined
				Set numeric value: row_index, "F3", 'f3:0'
			else
				Set string value: row_index, "F3", "NA"
			endif

		   endfor

	# etiket bos degilse kosulu bitir
	endif

   # donguyu bitir
   endfor
